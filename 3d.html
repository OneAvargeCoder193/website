<!DOCTYPE html>
<!--[if lt IE 7]>      <html class="no-js lt-ie9 lt-ie8 lt-ie7"> <![endif]-->
<!--[if IE 7]>         <html class="no-js lt-ie9 lt-ie8"> <![endif]-->
<!--[if IE 8]>         <html class="no-js lt-ie9"> <![endif]-->
<!--[if gt IE 8]>      <html class="no-js"> <!--<![endif]-->
<html>
    <head>
        <meta charset="utf-8">
        <meta http-equiv="X-UA-Compatible" content="IE=edge">
        <title></title>
        <meta name="description" content="">
        <meta name="viewport" content="width=device-width, initial-scale=1">
        <link rel="stylesheet" href="styles.css">
        <script src="src/gl-matrix.js"></script>
        <script src="src/utils.js"></script>
        <link rel="icon" type="image/x-icon" href="/favicon.png">
		<link rel="preconnect" href="https://fonts.googleapis.com">
		<link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
		<link href="https://fonts.googleapis.com/css2?family=Stack+Sans+Text:wght@200..700&display=swap" rel="stylesheet">
    </head>
    <body>
        <!--[if lt IE 7]>
            <p class="browsehappy">You are using an <strong>outdated</strong> browser. Please <a href="#">upgrade your browser</a> to improve your experience.</p>
        <![endif]-->
        <header id="header">
            <a href="./index.html" class="navigation"><div class="navdiv"><p class="navtext">Home</p></div></a>
            <a href="./about.html" class="navigation"><div class="navdiv"><p class="navtext">About</p></div></a>
            <a href="./3d.html" class="navigation"><div class="navdiv"><p class="navtext">3D Zone!</p></div></a>
            <a href="./voxelgame.html" class="navigation"><div class="navdiv"><p class="navtext">Voxel Game</p></div></a>
            <a href="./tailwind.html" class="navigation"><div class="navdiv"><p class="navtext">Tailwind</p></div></a>
        </header>
		<script type="x-shader/vs" id="vertex-colorgeo">
            #version 300 es

            layout(std140, column_major) uniform;
            
            layout(location=0) in vec4 position;
            layout(location=1) in vec2 uv;
            layout(location=2) in vec3 normal;
            layout(location=3) in vec4 color;
            layout(location=4) in mat4 modelMatrix;
            
            uniform SceneUniforms {
                mat4 uViewMatrix;
                mat4 uProjection;
                vec4 uEyePosition;
                vec4 uLightPosition;
            };

            uniform float uTime;
            
            out vec4 vPosition;
            out vec2 vUV;
            out vec4 vNormal;
            out vec4 vViewPosition;
            out vec4 vViewNormal;
			flat out int instanceId;
            
            float magicMath(float t) {
                t = 1.0 / (1.0 + exp(-5.0*t));
                return (smoothstep(0.0, 1.0, smoothstep(0.0, 1.0, smoothstep(0.0, 1.0, smoothstep(0.0, 1.0, t)))) + t) / 2.0;
            }

            float getAngle(float distance, float offset) {
                float a = magicMath(mod(uTime + offset/166.0*51.875, 51.875) - 25.9375);
                float b = smoothstep(0.0, 1.0, a);
                float t = clamp(distance / 5.0, 0.0, 1.0) * 2.0 - 1.0;
                return 3.14159*mix(a, b, t);
            }

            void main() {
                float distance = position.x * 5.0;
                float angle = getAngle(distance, float(gl_InstanceID));
                vec4 newPos = position;
				instanceId = gl_InstanceID;
                newPos.x = distance*cos(angle);
                newPos.y = distance*sin(angle);
                vPosition = modelMatrix * newPos;
                vNormal = modelMatrix * vec4(normal, 0.0);
                vUV = uv;
                vViewPosition = uViewMatrix * vPosition;
                vViewNormal = uViewMatrix * vNormal;
                gl_Position = uProjection * vViewPosition;
            }
        </script>
        <script type="x-shader/fs" id="fragment-colorgeo">
            #version 300 es
            precision highp float;
			precision highp sampler2DArray;

            layout(std140, column_major) uniform;

            uniform SceneUniforms {
                mat4 uViewMatrix;
                mat4 uProjection;
                vec4 uEyePosition;
                vec4 uLightPosition;
            };

            uniform sampler2DArray uTexture;
            
            in vec4 vPosition;
            in vec2 vUV;
            in vec4 vNormal;
            in vec4 vViewPosition;
            in vec4 vViewNormal;
			flat in int instanceId;

            layout(location=0) out vec4 color;
            layout(location=1) out vec4 viewPosition;
            layout(location=2) out vec4 viewNormal;

            void main() {
                vec3 position = vPosition.xyz;
                vec3 normal = normalize(vNormal.xyz);
                vec2 uv = vUV;
                if(!gl_FrontFacing) {
                    uv.x = 1.0-uv.x;
                }

                vec4 baseColor = texture(uTexture, vec3(uv, instanceId));
                vec3 eyeDirection = normalize(uEyePosition.xyz - position);
                vec3 lightVec = uLightPosition.xyz - position;
                vec3 lightDirection = normalize(lightVec);
                vec3 reflectionDirection = reflect(-lightDirection, normal);
                float nDotL = max(dot(lightDirection, normal), 0.0);
                float diffuse = nDotL;
                float ambient = 0.2;
                float specular = pow(max(dot(reflectionDirection, eyeDirection), 0.0), 20.0);

                color = vec4((ambient + diffuse + specular) * baseColor.rgb, 1.0);
                viewPosition = vViewPosition;
                viewNormal = vViewNormal;
            }
        </script>
        <script type="x-shader/vs" id="vertex-quad">
            #version 300 es

            layout(location=0) in vec4 position;
            
            void main() {
                gl_Position = position;
            }
        </script>

        <script type="x-shader/fs" id="fragment-ssao">
            #version 300 es
            precision highp float;

            layout(std140, column_major) uniform;

            #define SIN45 0.707107

            uniform SSAOUniforms {
                float uSampleRadius;
                float uBias;
                vec2 uAttenuation;
                vec2 uDepthRange;
            };

            uniform sampler2D uPositionBuffer;
            uniform sampler2D uNormalBuffer;
            uniform sampler2D uNoiseBuffer;
            
            out float occlusion;

            float getOcclusion(vec3 position, vec3 normal, ivec2 fragCoord) {
                vec3 occluderPosition = texelFetch(uPositionBuffer, fragCoord, 0).xyz;
                vec3 positionVec = occluderPosition - position;
                float intensity = max(dot(normal, normalize(positionVec)) - uBias, 0.0);
            
                float attenuation = 1.0 / (uAttenuation.x + uAttenuation.y * length(positionVec));

                return intensity * attenuation;
            }

            void main() {
                ivec2 fragCoord = ivec2(gl_FragCoord.xy);
                vec3 position = texelFetch(uPositionBuffer, fragCoord, 0).xyz;
                vec3 normal = texelFetch(uNormalBuffer, fragCoord, 0).xyz;
                vec2 rand = normalize(texelFetch(uNoiseBuffer, fragCoord, 0).xy);
                float depth = (length(position) - uDepthRange.x) / (uDepthRange.y - uDepthRange.x);

                float kernelRadius = uSampleRadius * (1.0 - depth);

                vec2 kernel[4];
                kernel[0] = vec2(0.0, 1.0);
                kernel[1] = vec2(1.0, 0.0);
                kernel[2] = vec2(0.0, -1.0);
                kernel[3] = vec2(-1.0, 0.0);

                occlusion = 0.0;
                for (int i = 0; i < 4; ++i) {
                    vec2 k1 = reflect(kernel[i], rand);
                    vec2 k2 = vec2(k1.x * SIN45 - k1.y * SIN45, k1.x * SIN45 + k1.y * SIN45);

                    k1 *= kernelRadius;
                    k2 *= kernelRadius;

                    occlusion += getOcclusion(position, normal, fragCoord + ivec2(k1));
                    occlusion += getOcclusion(position, normal, fragCoord + ivec2(k2 * 0.75));
                    occlusion += getOcclusion(position, normal, fragCoord + ivec2(k1 * 0.5));
                    occlusion += getOcclusion(position, normal, fragCoord + ivec2(k2 * 0.25));
                }

                occlusion = clamp(occlusion / 16.0, 0.0, 1.0);
            }
        </script>
        <script type="x-shader/fs" id="fragment-aoblend">
            #version 300 es
            precision highp float;

            uniform sampler2D uColorBuffer;
            uniform sampler2D uOcclusionBuffer;

            out vec4 color;
            void main() {
                ivec2 fragCoord = ivec2(gl_FragCoord.xy);
                color = vec4(clamp(texelFetch(uColorBuffer, fragCoord, 0).rgb - texelFetch(uOcclusionBuffer, fragCoord, 0).r, 0.0, 1.0), 1.0);
            }
        </script>
        <script type="x-shader/fs" id="fragment-color">
            #version 300 es
            precision highp float;

            uniform sampler2D uColorBuffer;

            out vec4 color;

            void main() {
                color = vec4(texelFetch(uColorBuffer, ivec2(gl_FragCoord.xy), 0).rgb, 1.0);
            }
        </script>
        <canvas id="canvas"></canvas>
        <script src="./src/3d.js"></script>
    </body>
</html>